#!/usr/bin/perl

use lib "$ENV{GUS_HOME}/lib/perl";
use strict;
use Getopt::Long;
use Time::Local;
use FgpUtil::Util::SlowQueryReport;

my ($threshold, $genepage, $notgenepage, $width_filter, $time_filter, $sort_column, $plotOutputFile, $logTailSize, $logDeathImmunity, $debug, $tabfile, $brief);
my ($width_min, $width_max);

usage() unless scalar(@ARGV);

$sort_column=2;
&GetOptions('s=s' => \$threshold,
	    'g' => \$genepage,
	    'n' => \$notgenepage,
	    'w=s' => \$width_filter,
	    't=s' => \$time_filter,
	    'c=s' => \$sort_column,
	    'p=s' => \$plotOutputFile,
	    'l=s' => \$logTailSize,
	    'i' => \$logDeathImmunity,
            'd' => \$debug,
            'b' => \$brief,
            'f=s' => \$tabfile,
	   );

if ($width_filter) {
  ($width_min, $width_max) = split(/,\s*/, $width_filter);
}

my $parseLogRecord = sub {
  my ($logRecord) = @_;

  my $reject = 0;

  # QUERYTIME       Fri Mar 12 22:26:49 2010        1268450809.72195        baseline       Segment.pm      0.004    7.12     5792    alignment:dbEST EOL

  $reject = 1 unless $logRecord =~ /QUERYTIME/;
  $reject = 1 if $logRecord =~ /EOL./;  # some lines in the log are mangled, with missing newlines.  die if there is an EOL that is not at the end of the line.
  $reject = 1 if ($genepage && !$logRecord =~ /GENEPAGE/);
  $reject = 1 if ($notgenepage && $logRecord =~ /GENEPAGE/);
  chomp($logRecord);
  my ($qt, $timestampString, $timestamp, $tag, $module, $executeSeconds, $lastPageSeconds, $width, $name) = split(/\t/, $logRecord);

  $reject = 1 if ($width_min && $width < $width_min);
  $reject = 1 if ($width_max && $width > $width_max);
  $reject = 1 if ($timestamp =~ /[a-z,A-Z]/); # this happened: the "time" field had a word embedded in it

  return ($reject, $timestamp, $lastPageSeconds, $name, $width);
};

FgpUtil::Util::SlowQueryReport::makeReport($parseLogRecord, $time_filter, $plotOutputFile, $sort_column, $logTailSize, $logDeathImmunity, $threshold, $debug, $tabfile, $brief);

sub usage {
  print STDERR "

Print a report summarizing the gbrowse slow query logs.

Takes one or more apache error logs on standard input.

usage:  gbrowseSlowQueryReport -s secs [-g] [-n] [-w width_min[,length_max]] [-t starttime[,endtime]] [-c colnum] [-p plotOutputFile] [-f tabOutputFile] [-d] [-i] [-b]

where:
  -s:  slowness threshold in seconds.  run times over secs are reported in Slow columns
  -g:  genepage only flag. only include queries logged with \"GENEPAGE\"
  -n:  not-genepage flag. complementary to -g flag (only non-genepage queries)
  -w:  width filter
  -t:  time filter.  Use the time format as found in the WDK slow query log, eg, 02/Jun/2013:23:41:28, or seconds since epoch (found in GBrowse log).
  -c:  column to sort on (default is 2, the total time)
  -p:  optional output file: a tab delimited file to pass as input
        to wdkSlowQueryPlot.  Used to visualize the query durations over time.
  -l:  log tail size -- number of records of access log to analyze for the page report (default=0, ie, skip this report)
  -d:  print debugging info
  -b:  brief output: skip last two columns (server and log file) so that output doesn't wrap
  -i:  immunity from log death: don't die if the log file doesn't cover the period of interest.  (Will only die if -l option is set)
  -f:  optional file name to write the report in tab-delimited format (for import to excel)

Standard in must contain one or more lines specifying log files.   Each line has three columns, tab delimited:
  - server name
  - log file glob
  - access log file name

gbrowseSlowQueryReport uses ssh to log in to that server and read all the log files implied by the log file glob.

The access log is used to get a count of pages, which are shown in the page-requests column in the Statistics section at the 
bottom of the report.  This is useful if we know that certain queries only happen on that page.  Right now this is hard coded 
to look for EuPathDB gene pages.  It is useful for the gbrowseSlowQueryReport which tags queries as belonging to the gene page.  

";
  exit(1);
}
